/*
 * Copyright (C) 2015 Jorge Ruesga
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle-experimental:0.7.2'
    }
}

apply plugin: 'com.android.model.application'

repositories {
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.github.danielnilsson9:color-picker-view:1.4.0'
    compile ('org.adw.library:discrete-seekbar:1.0.0') { transitive = false }
    compile ('com.github.chrisbanes:PhotoView:1.2.6') { transitive = false }
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.squareup.okio:okio:1.8.0'
    compile ('com.mikepenz:fastadapter:1.5.1') { transitive = false }
    compile ('com.mikepenz:iconics-core:2.5.11') { transitive = false }
    compile ('com.mikepenz:aboutlibraries:5.6.6') { transitive = false }
}

model {
    android {
        compileSdkVersion 23
        buildToolsVersion "23.0.3"

        defaultConfig {
            versionCode 1020
            versionName "2.1.1"

            minSdkVersion.apiLevel 15
            targetSdkVersion.apiLevel 23
        }

        ndk {
            moduleName = "photophase"
            platformVersion =  16
            toolchain = 'clang'
            CFlags.addAll(['-Wall'])
            ldLibs.addAll(['GLESv2'])
        }

        buildTypes {
            debug {
                debuggable true
                ndk {
                    debuggable true
                }
            }
            release {
                debuggable false
                ndk {
                    debuggable false
                }

                signingConfig = $("android.signingConfigs.release-signing-config")

                zipAlignEnabled true
                shrinkResources false
                minifyEnabled true
                useProguard true

                proguardFiles.addAll([file('proguard-android.txt'), file('proguard-project.txt')])
            }
        }

        compileOptions.with {
            sourceCompatibility = JavaVersion.VERSION_1_7
            targetCompatibility = JavaVersion.VERSION_1_7
        }
    }

    android.lintOptions {
        disable 'MissingTranslation', 'ManifestResource'
    }

    android.signingConfigs {
        create("release-signing-config") {
            def Properties releaseProps = new Properties()
            releaseProps.load(new FileInputStream(rootProject.file('release.properties')))

            def Properties ksProps = new Properties()
            ksProps.load(new FileInputStream(new File((String) releaseProps['ruesga.com.config'])))
            storeFile file(ksProps["keystore"])
            keyAlias ksProps["alias"]
            storePassword ksProps["storePass"]
            keyPassword ksProps["pass"]
            storeType ksProps["storeType"]
        }
    }
}