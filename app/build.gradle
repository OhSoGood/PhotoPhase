/*
 * Copyright (C) 2015 Jorge Ruesga
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle-experimental:0.7.2'
    }
}

apply plugin: 'com.android.model.application'

repositories {
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile 'com.android.support:design:24.1.1'
    compile 'com.android.support:cardview-v7:24.1.1'
    compile 'com.github.danielnilsson9:color-picker-view:1.4.0'
    compile ('org.adw.library:discrete-seekbar:1.0.1') { transitive = false }
    compile ('com.github.chrisbanes:PhotoView:1.2.6') { transitive = false }
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.okio:okio:1.9.0'
    compile ('com.mikepenz:fastadapter:1.6.0') { transitive = false }
    compile ('com.mikepenz:iconics-core:2.6.7') { transitive = false }
    compile ('com.mikepenz:aboutlibraries:5.7.1') { transitive = false }

    // Chromecast
    compile 'com.google.android.gms:play-services-gcm:9.4.0'
    compile ('com.github.jruesga:chromecast-java-api-v2:e189575082') { transitive = false }
    compile ('com.google.protobuf:protobuf-java:2.6.1') { transitive = false }
    compile ('org.codehaus.jackson:jackson-core-asl:1.9.13') { transitive = false }
    compile ('org.codehaus.jackson:jackson-mapper-asl:1.9.13') { transitive = false }
    compile 'org.nanohttpd:nanohttpd:2.3.0'
}

model {
    android {
        compileSdkVersion 24
        buildToolsVersion "24.0.1"

        defaultConfig {
            versionCode 1024
            versionName "2.2.3"

            minSdkVersion.apiLevel 15
            targetSdkVersion.apiLevel 24
        }

        ndk {
            moduleName = "photophase"
            platformVersion =  15
            toolchain = 'clang'
            CFlags.addAll(['-Wall'])
            ldLibs.addAll(['GLESv2'])
        }

        buildTypes {
            debug {
                debuggable true
                ndk {
                    debuggable true
                }
            }
            release {
                debuggable false
                ndk {
                    debuggable false
                }

                signingConfig = $("android.signingConfigs.release-signing-config")

                zipAlignEnabled true
                shrinkResources false
                minifyEnabled true
                useProguard true

                proguardFiles.addAll([file('proguard-android.txt'), file('proguard-project.txt')])
            }
        }

        compileOptions.with {
            sourceCompatibility = JavaVersion.VERSION_1_7
            targetCompatibility = JavaVersion.VERSION_1_7
        }

        packagingOptions.with {
            // Ignore duplicate apache license files which breaks the AS build
            exclude 'META-INF/ASL2.0'
        }
    }

    android.lintOptions {
        disable 'MissingTranslation', 'ManifestResource'
    }

    android.signingConfigs {
        create("release-signing-config") {
            def Properties releaseProps = new Properties()
            releaseProps.load(new FileInputStream(rootProject.file('release.properties')))

            def Properties ksProps = new Properties()
            ksProps.load(new FileInputStream(new File((String) releaseProps['ruesga.com.config'])))
            storeFile file(ksProps["keystore"])
            keyAlias ksProps["alias"]
            storePassword ksProps["storePass"]
            keyPassword ksProps["pass"]
            storeType ksProps["storeType"]
        }
    }
}