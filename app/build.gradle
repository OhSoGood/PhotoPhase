/*
 * Copyright (C) 2015 Jorge Ruesga
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle-experimental:0.8.3'
    }
}

apply plugin: 'com.android.model.application'

repositories {
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile 'com.android.support:design:25.1.1'
    compile 'com.android.support:cardview-v7:25.1.1'
    compile 'com.android.support:exifinterface:25.1.1'
    compile 'com.github.danielnilsson9:color-picker-view:1.4.0'
    compile ('org.adw.library:discrete-seekbar:1.0.1') { transitive = false }
    compile ('com.github.chrisbanes:PhotoView:1.3.1') { transitive = false }
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile 'com.squareup.okio:okio:1.11.0'
    compile ('com.mikepenz:fastadapter:2.1.5') { transitive = false }
    compile ('com.mikepenz:iconics-core:2.8.1') { transitive = false }
    compile ('com.mikepenz:aboutlibraries:5.9.1') { transitive = false }

    // Chromecast
    compile 'com.google.android.gms:play-services-gcm:10.0.1'
    compile ('com.github.jruesga:chromecast-java-api-v2:2f777b443c') { transitive = false }
    compile ('com.google.protobuf:protobuf-java:2.6.1') { transitive = false }
    compile ('org.codehaus.jackson:jackson-core-asl:1.9.13') { transitive = false }
    compile ('org.codehaus.jackson:jackson-mapper-asl:1.9.13') { transitive = false }
    compile 'org.nanohttpd:nanohttpd:2.3.1'
}

model {
    android {
        compileSdkVersion 25
        buildToolsVersion "25.0.2"

        defaultConfig {
            versionCode 1029
            versionName "2.4.2"

            minSdkVersion.apiLevel 15
            targetSdkVersion.apiLevel 25
        }

        ndk {
            moduleName = "photophase"
            platformVersion =  15
            toolchain = 'clang'
            CFlags.addAll(['-Wall'])
            ldLibs.addAll(['GLESv2'])
        }

        buildTypes {
            debug {
                debuggable true
                ndk {
                    debuggable true
                }
            }
            release {
                debuggable false
                ndk {
                    debuggable false
                }

                signingConfig = $("android.signingConfigs.release-signing-config")

                zipAlignEnabled true
                shrinkResources false
                minifyEnabled true
                useProguard true

                proguardFiles.addAll([file('proguard-android.txt'), file('proguard-project.txt')])
            }
        }

        compileOptions.with {
            sourceCompatibility = JavaVersion.VERSION_1_7
            targetCompatibility = JavaVersion.VERSION_1_7
        }

        packagingOptions.with {
            // Ignore duplicate apache license files which breaks the AS build
            exclude 'META-INF/ASL2.0'
            exclude 'META-INF/LICENSE'
        }
    }

    android.lintOptions {
        disable 'MissingTranslation', 'ManifestResource'
    }

    android.signingConfigs {
        create("release-signing-config") {
            def Properties releaseProps = new Properties()
            releaseProps.load(new FileInputStream(rootProject.file('release.properties')))

            def Properties ksProps = new Properties()
            ksProps.load(new FileInputStream(new File((String) releaseProps['signing.config'])))
            storeFile file(ksProps["keystore"])
            keyAlias ksProps["alias"]
            storePassword ksProps["storePass"]
            keyPassword ksProps["pass"]
            storeType ksProps["storeType"]
        }
    }
}