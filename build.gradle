/*
 * Copyright (C) 2015 Jorge Ruesga
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext {
    libraries = [
            support                     : '25.2.0',
            gms                         : '10.2.0',
            okhttp3                     : '3.6.0',
            okio                        : '1.11.0',
            colorpickerview             : '1.4.0',
            discrete_seekbar            : '1.0.1',
            photoview                   : '1.3.1',
            aboutlibraries              : '5.9.4',
            chromecast                  : '2f777b443c',
            protobuf                    : '2.6.1',
            jackson                     : '1.9.13',
            nanohttpd                   : '2.3.1'
    ]

    testLibraries = [
    ]

    sdkVersions = [
            compileSdkVersion           : 25,
            buildToolsVersion           : '25.0.2',
            minSdkVersion               : 15,
            targetSdkVersion            : 25
    ]
}

allprojects {
    buildscript {
        repositories {
            jcenter()
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:2.3.0'
            classpath 'com.android.tools.build:gradle-experimental:0.9.0'
            classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.2'
        }
    }

    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

subprojects {
    afterEvaluate {project ->
        if (project.plugins.hasPlugin('android') || project.plugins.hasPlugin('android-library')) {
            android {
                compileSdkVersion rootProject.ext.sdkVersions.compileSdkVersion
                buildToolsVersion rootProject.ext.sdkVersions.buildToolsVersion

                defaultConfig {
                    minSdkVersion rootProject.ext.sdkVersions.minSdkVersion
                    targetSdkVersion rootProject.ext.sdkVersions.targetSdkVersion

                    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility = JavaVersion.VERSION_1_7
                    targetCompatibility = JavaVersion.VERSION_1_7
                }

                packagingOptions {
                    // Ignore duplicate apache license files which breaks the AS build
                    exclude 'META-INF/ASL2.0'
                    exclude 'META-INF/LICENSE'
                }
            }
        }
    }
}
